apply plugin: "com.android.application"
apply plugin: "android-apt"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        def versionMajor = 1
        def versionMinor = 0
        def versionPatch = 0
        applicationId "org.hawkular.client.android"

        versionName buildVersionName(versionMajor, versionMinor, versionPatch)
        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch)

        minSdkVersion 16
        targetSdkVersion 25

        resConfigs "en", "de"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        def serverProperties = readProperties("server.properties")

        buildConfigField "String", "UPS_URL", formatStringField(readProperty(serverProperties, "ups.url"))
        buildConfigField "String", "UPS_VARIANT", formatStringField(readProperty(serverProperties, "ups.variant"))
        buildConfigField "String", "UPS_SECRET", formatStringField(readProperty(serverProperties, "ups.secret"))
        buildConfigField "String", "FCM_SENDER", formatStringField(readProperty(serverProperties, "fcm.sender"))
        buildConfigField "String", "FCM_APPLICATION_ID", formatStringField(readProperty(serverProperties, "fcm" +
                ".application"))
        buildConfigField "String", "FCM_API_KEY", formatStringField(readProperty(serverProperties, "fcm.api"))
        buildConfigField "String", "FCM_DATABASE_URL", formatStringField(readProperty(serverProperties, "fcm.database"))
        buildConfigField "String", "FCM_STORAGE_BUCKET", formatStringField(readProperty(serverProperties, "fcm.bucket"))

        signingConfigs {
            release {
                if (file("signing.properties").exists()) {
                    def signingProperties = readProperties("signing.properties")

                    storeFile file(readProperty(signingProperties, "keystore.file"))
                    storePassword readProperty(signingProperties, "keystore.password")
                    keyAlias readProperty(signingProperties, "key.alias")
                    keyPassword readProperty(signingProperties, "key.password")
                }
            }
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }


        buildTypes {
            debug {
                minifyEnabled true
                def files = readProguardFiles()
                files.each { file ->
                    proguardFiles file
                }

            }

            release {
                signingConfig signingConfigs.release

                minifyEnabled true
                shrinkResources true
                def files = readProguardFiles()
                files.each { file ->
                    proguardFiles file
                }
            }
        }
    }
}

def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

def buildVersionCode(versionMajor, versionMinor, versionPatch) {
    return versionMajor * 10 ** 2 + versionMinor * 10 ** 1 + versionPatch * 10 ** 0
}

def readProperties(propertiesFileName) {
    def properties = new Properties()
    def propertiesFile = file(propertiesFileName)

    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newInputStream())
    }

    return properties
}

def readProperty(properties, propertyName) {
    def property = properties[propertyName]

    if (property) {
        return property
    } else {
        return ""
    }
}

def formatStringField(field) {
    return "\"${field}\""
}

def readProguardFiles() {
    def proguardFiles = []

    proguardFiles << android.getDefaultProguardFile("proguard-android.txt")

    file("proguard").eachFile { proguardFile ->
        proguardFiles << proguardFile
    }

    return proguardFiles
}

dependencies {

    wearApp project(':wear')
    compile 'com.google.android.gms:play-services:10.2.0'

    apt "com.jakewharton:butterknife-compiler:8.0.1"

    compile "com.android.support:appcompat-v7:25.1.1"
    compile "com.android.support:design:25.1.1"
    compile "com.android.support:preference-v7:25.1.1"
    compile "com.android.support:support-annotations:25.1.1"
    compile "com.android.support:support-v4:25.1.1"
    compile 'com.github.bmelnychuk:atv:1.2.8'
    compile 'com.github.johnkil.print:print:1.2.2'
    compile "com.github.lecho:hellocharts-library:1.5.8@aar"
    compile "com.jakewharton:butterknife:8.0.1"
    compile "com.jakewharton.timber:timber:3.1.0"
    compile "com.squareup.okhttp:okhttp:2.7.5"
    compile "com.squareup.okhttp:okhttp-ws:2.7.5"
    compile "info.metadude.android:typed-preferences:2.0.0"
    compile "org.jboss.aerogear:aerogear-android-authz:3.0.0"
    compile "org.jboss.aerogear:aerogear-android-pipe:3.1.0"
    compile "org.jboss.aerogear:aerogear-android-push:4.0.2"
    compile "com.squareup:otto:1.3.8"
    compile "frankiesardo:icepick:3.1.0"

    provided "frankiesardo:icepick-processor:3.1.0"

    testCompile 'junit:junit:4.12'
    androidTestCompile "com.android.support:support-annotations:25.1.1"
    androidTestCompile "com.android.support.test:rules:0.5"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.squareup.assertj:assertj-android:1.1.0"

}